#!/usr/bin/env perl
use strict;
use warnings;
use 5.010;
use Data::Dumper;
use FindBin '$Bin';
use YAML::XS qw/ LoadFile /;
use IO::All;
use File::Copy qw/ copy /;
use HTML::Template::Compiled;
use POSIX qw/ strftime /;
use Encode;
use FindBin '$Bin';
use lib "$Bin/../lib/perl5";
use YAML::Matrix qw/
    gather_tags
/;

my %tests;
my %frameworks;
my @frameworks;
my %framework_info;
my %overview;
my %htc_args = (
    tagstyle => [qw/ -classic -comment -jsp +tt /],
    default_escape => 'HTML',
    loop_context_vars => 1,
    post_chomp => 2,
    use_expressions => 1,
);

my @status = (qw/ ok json_ok diff json_diff na error correct/);

my $datadir = "$Bin/../yaml-test-suite/data";

my $id2tag = gather_tags("$datadir/tags");
gather_data();

create_html();

create_overview();

sub gather_data {
    my @files = map { "$_" } grep { not m/-out/ } grep { m/\.yaml$/ } io->dir("matrix/data")->all_files;

    for my $file (sort @files) {
        unless ($file =~ m{/([A-Z0-9-]+)\.yaml}i) {
            next;
        }
        my $fw = $1;
        push @frameworks, $fw;
        my $data = LoadFile($file);
        my $testdata = $data->{tests};
        my $info = $data->{info};
        $info->{run} = $data->{date};
        $info->{runtime} = $data->{time};
        $framework_info{ $fw } = info2html($info);
        # issue with yaml-editor build process
        if ($framework_info{ $fw }->{version}) {
            $framework_info{ $fw }->{version} =~ s/-dirty$//;
        }

        for my $test (sort keys %$testdata) {
            my $results = $testdata->{ $test };
            my $tags = $id2tag->{ $test } || [];
            @$tags = sort @$tags;
            if ($results->{invalid}) {
                for (qw/ invalid_correct invalid_incorrect /) {
                    if ($results->{ $_ }) {
                        $overview{ $fw }->{ $_ }++;
                    }
                }
                next;
            }
            my $title = $results->{title};
            $tests{ $test }->{id} = $test;
            $tests{ $test }->{title} = $title;
            $tests{ $test }->{frameworks}->{ $fw } = $results;
            $tests{ $test }->{frameworks}->{ $fw }->{id} = $fw;
            $tests{ $test }->{tags} = $tags;
            for my $status (@status) {
                if ($results->{ $status }) {
                    $frameworks{ $fw }->{ $status }++;
                    $overview{ $fw }->{ $status }++;
                    for my $tag (@$tags) {
                        $overview{ $fw }->{tags}->{ $status }->{ $tag }++;
                    }
                    my $by_status = $tests{ $test }->{framework_by_status}->{ $status } ||= [];
                    push @{ $by_status }, { framework => $fw, results => $results };

                }
            }
            $frameworks{ $fw }->{count}++;

        }
    }
}

sub create_html {
    mkdir "matrix/html";
    mkdir "matrix/html/details";
    mkdir "matrix/html/css";
    mkdir "matrix/html/js";
    my @tests;

    my @framework_names = map {
        my ($lang, $name, $type) = split /-/, $_;
        if ($lang eq 'libyaml') {
            $name = $lang;
            $lang = 'C';
            $type = 'event';
        }
        elsif ($lang eq 'ruby' and $name eq 'json') {
            $type = $name;
            $name = "yaml";
        }
        elsif ($lang eq 'perl6') {
            $type = $name;
            $name = "yamlish";
        }
        elsif ($lang =~ /^(pyyaml|ruamel)$/) {
            $type = $name;
            $name = $lang;
            $lang = "python";
        }
        elsif ($lang eq 'java') {
            $type = $name;
            $name = "snake";
        }
        elsif ($lang eq 'nimyaml') {
            $lang = "nim";
            $type = $name;
            $name = "yaml";
        }
        elsif ($lang eq 'cpp') {
            $lang = "C++";
            $name = "yaml";
            $type = "event";
        }
        elsif ($lang eq 'luajit') {
            $lang = "lua";
            $name = "yaml";
            $type = "json";
        }
        if ($type !~ m/^(json|event)/) {
            $type = "native";
        }
        {
            lang => $lang,
            name => $name,
            type => $type,
            fullname => $_,
        };
    } @frameworks;
    @framework_names = sort {
        $a->{type} cmp $b->{type}
        || $a->{lang} cmp $b->{lang}
        || $a->{name} cmp $b->{name}
    } @framework_names;


    for my $test (sort { $tests{ $a }->{title} cmp $tests{ $b }->{title}} keys %tests) {
        my $data = $tests{ $test };
        my $frameworks = $data->{frameworks};
        my @frameworks;
        for my $fw (@framework_names) {
            my $results = $frameworks->{ $fw->{fullname} };
            $results->{test} = $test;
            if (exists $results->{got_json}) {
                $results->{got_json_lines} = [split m/\n/, $results->{got_json} ];
            }
            push @frameworks, $results;
        }
        push @tests, {
            id => $data->{id},
            title => $data->{title},
            frameworks => \@frameworks,
        };
        create_details($test);
    }
    say "Created detail pages matrix/html/details/<id>.html";
    my @framework_headers;
    for my $fw (@framework_names) {
        push @framework_headers, {
            fullname => $fw->{fullname},
            %{ $frameworks{ $fw->{fullname} } || {} },
        };
    }

    my %selectors;
    for my $fw (@framework_names) {
        push @{ $selectors{ $fw->{type} } }, $fw;
    }
    my @selectors = map {
        +{
            type => $_,
            frameworks => $selectors{ $_ }
        },
    } sort {
        $selectors{ $a }->[0]->{type}
            cmp
        $selectors{ $b }->[0]->{type}
    } keys %selectors;

    {
        my $htc = HTML::Template::Compiled->new(
            filename => "valid.html",
            path => "$Bin/../share/matrix/templates",
            %htc_args,
        );
        $htc->param(
            framework_names => \@framework_names,
            framework_headers => \@framework_headers,
            tests => \@tests,
            selectors => \@selectors,
        );

        open my $fh, ">", "matrix/html/valid.html" or die $!;
        print $fh $htc->output;
        close $fh;
        say "Created matrix/html/valid.html";
    }
    {
        my $htc = HTML::Template::Compiled->new(
            filename => "frameworks.html",
            path => "$Bin/../share/matrix/templates",
            %htc_args,
        );
        $htc->param(
            framework_info => \%framework_info,
        );

        open my $fh, ">", "matrix/html/frameworks.html" or die $!;
        print $fh $htc->output;
        close $fh;
        say "Created matrix/html/frameworks.html";
    }

    copy ("share/matrix/css/yaml.css", "matrix/html/css");
    copy ("share/matrix/css/matrix.css", "matrix/html/css");
    copy ("share/matrix/css/sheet.css", "matrix/html/css");
    copy ("share/matrix/css/yaml-tokens.css", "matrix/html/css");
    copy ("share/matrix/js/jquery-3.1.1.min.js", "matrix/html/js");
    copy ("share/matrix/js/matrix.js", "matrix/html/js");
    copy ("share/matrix/js/sheet.js", "matrix/html/js");
}

sub create_overview {

    my $git_cmd = qq{cd $datadir; git log -1 --pretty="format:%h %ad %s" --date=short};
    chomp(my $data_commit = qx{$git_cmd});
    my ($sha, $date, $msg) = split ' ', $data_commit, 3;


    my @overview;
    my ($sum_valid, $sum_invalid) = (0, 0);
    my $factor = 1.7;
    for my $fw (sort {
        my @parts_a = split m/-/, $a;
        my @parts_b = split m/-/, $b;
        $parts_a[-1] cmp $parts_b[-1] ||
        $a cmp $b
    } keys %overview) {
        my $data = $overview{ $fw };
        my %row;
        $row{framework} = $fw;
        $row{framework_info} = $framework_info{ $fw };
        my $total_valid = 0;
        my $total_invalid = 0;
        for my $status (qw/
            ok json_ok diff json_diff na error
            invalid_correct invalid_incorrect /) {
            my $count = $data->{ $status };
            $row{ $status } = $count;
            $count ||= 0;
            $total_valid += $count unless $status =~ m/invalid/;
            $total_invalid += $count if $status =~ m/invalid/;
        }
        my @percentage_valid;
        my @percentage_invalid;
        my @status_percentage_valid;
        my @status_percentage_invalid;
        if ($fw =~ m/-event$/) {
            @status_percentage_valid = qw/ ok diff error /;
            @status_percentage_invalid = qw/ invalid_correct invalid_incorrect /;
        }
        elsif ($fw =~ m/-json$/) {
            @status_percentage_valid = qw/ json_ok na json_diff error /;
            @status_percentage_invalid = qw/ invalid_correct invalid_incorrect /;
        }
        else {
            @status_percentage_valid = qw/ na error /;
            @status_percentage_invalid = qw/ invalid_correct invalid_incorrect /;
        }
        for my $i (0 .. $#status_percentage_valid) {
            my $status = $status_percentage_valid[ $i ];
            my $percent = sprintf "%.2f",
                ($data->{ $status } || 0);
            my $border = ( $i == 0 )
                    ? 'left'
                    : ($i == $#status_percentage_valid)
                        ? 'right'
                        : 'middle';
            push @percentage_valid, {
                status => $status,
                percent => $percent,
                border => $border,
                width => $percent * $factor,
            };
        }
        for my $i (0 .. $#status_percentage_invalid) {
            my $status = $status_percentage_invalid[ $i ];
            my $percent = sprintf "%.2f",
                ($data->{ $status } || 0);
            my $border = ( $i == 0 )
                    ? 'left'
                    : ($i == $#status_percentage_invalid)
                        ? 'right'
                        : 'middle';
            push @percentage_invalid, {
                status => $status,
                percent => $percent,
                border => $border,
                width => $percent * $factor,
            };
        }
        $row{percentage_valid} = \@percentage_valid;
        $row{percentage_invalid} = \@percentage_invalid;
        $sum_valid = $total_valid;
        $sum_invalid = $total_invalid;
        push @overview, \%row;
    }
    my $htc = HTML::Template::Compiled->new(
        filename => "index.html",
        path => "$Bin/../share/matrix/templates",
        %htc_args,
    );
    $htc->param(
        overview => \@overview,
        sum_valid => $sum_valid,
        sum_invalid => $sum_invalid,
        width_valid => $sum_valid * $factor + 10,
        width_invalid => $sum_invalid * $factor + 10,
        data_commit => {
            sha => $sha,
            date => $date,
            msg => $msg,
        },
    );
    open my $fh, ">", "matrix/html/index.html" or die $!;
    print $fh $htc->output;
    close $fh;
    say "Created matrix/html/index.html";
}

sub create_details {
    my ($id) = @_;
    my $yaml = io->file("$datadir/$id/in.yaml")->encoding('utf-8')->slurp;
    my $events = io->file("$datadir/$id/test.event")->encoding('utf-8')->slurp;

    my $json;
    my $json_lines;
    if (-f "$datadir/$id/in.json") {
        $json = io->file("$datadir/$id/in.json")->slurp;
        $json_lines = [ split m/\n/, $json ];
    }
    my $test = $tests{ $id };
    my $by_status = $test->{framework_by_status};
    my @ordered_by_status;
    for my $status (qw/ ok json_ok diff json_diff error na invalid_correct invalid_incorrect /) {
        my $list = $by_status->{ $status };
        my @states;
        for my $item (@$list) {
            if ($item->{results}->{ $status }) {
                $item->{results}->{status} = $status;
            }
            push @states, $item;
        }
        push @ordered_by_status, \@states;
    }
    $test->{ordered_by_status} = \@ordered_by_status;

    my $htc = HTML::Template::Compiled->new(
        filename => "details.html",
        path => "$Bin/../share/matrix/templates",
        %htc_args,
    );
    $htc->param(
        id => $id,
        test => $test,
        yaml => $yaml,
        json => $json,
        json_lines => $json_lines,
        events => $events,
    );
    open my $fh, ">", "matrix/html/details/$id.html" or die $!;
    print $fh encode_utf8 $htc->output;
    close $fh;
}

sub info2html {
    my ($info) = @_;
    return unless $info;
    $info->{run_date} = strftime("%Y-%m-%d %H:%M GMT", gmtime $info->{run});
    return $info unless $info->{commit};

    my $link;
    my $commitlink;
    $info->{commit_short} = substr($info->{commit}, 0, 8);
    my $provider = $info->{provider} // '';
    if ($provider eq 'github') {
        $link = "https://github.com/$info->{reponame}";
        $commitlink =
            "https://github.com/$info->{reponame}/commit/$info->{commit}";
    }
    elsif ($provider eq 'bitbucket') {
        $link = "https://bitbucket.org/$info->{reponame}";
        $commitlink =
            "https://bitbucket.org/$info->{reponame}/commits/$info->{commit}";
    }
    $info->{link} = $link;
    $info->{commit_link} = $commitlink;

    $info->{language_name} = {
        cpp => "C++",
        c => "C",
        haskell => "Haskell",
        java => "Java",
        js => "Javascript",
        lua => "Lua",
        nim => "Nim",
        perl5 => "Perl 5",
        perl6 => "Perl 6",
        python => "Python",
        ruby => "Ruby",
    }->{ $info->{language} };

    $info->{output_name} = {
        event => "Event Stream",
        json => "JSON",
        native => "Native Data",
    }->{ $info->{output} };

    return $info;
}
